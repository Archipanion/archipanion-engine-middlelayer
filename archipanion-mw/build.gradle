
plugins {
    id "org.jetbrains.kotlin.jvm" version "$version_kotlin"
    id "org.jetbrains.kotlin.plugin.serialization" version "$version_kotlin"
    id 'java-library'
    id 'idea'
    id 'org.openapi.generator' version '6.3.0'
    id "de.undercouch.download" version "5.4.0"
}


kotlin {
    jvmToolchain(21)
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

def engineBuild = getLayout().buildDirectory.dir("/generated/main/kotlin/engine").get().asFile.toString()

sourceSets {
    main {
        resources {
            srcDir engineBuild
        }
    }
}

dependencies {
    testImplementation("org.jetbrains.kotlin:kotlin-test")

    /* Kotlinx Extensions */
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: version_kotlinx_coroutines
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json-jvm', version: version_kotlinx_json_jvm


    /* Caffeine */
    implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: version_caffeine

    /* Logging */
    implementation group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
    implementation group: 'io.github.oshai', name: 'kotlin-logging-jvm', version: version_kotlinlogging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j2
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: version_log4j2

    /** Clikt & JLine */
    implementation group: 'com.github.ajalt.clikt', name: 'clikt', version: version_clikt
    implementation group: 'org.jline', name: 'jline', version: version_jline
    implementation group: 'org.jline', name: 'jline-terminal-jna', version: version_jline
    implementation group: 'com.jakewharton.picnic', name: 'picnic', version: version_picnic


    /** Javalin */
    implementation group: 'io.javalin', name: 'javalin', version: version_javalin
    implementation group: 'io.javalin.community.openapi', name: 'javalin-openapi-plugin', version: version_javalin
    implementation group: 'io.javalin.community.ssl', name: 'ssl-plugin', version: version_javalin

    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'

}

def oasFile = "https://raw.githubusercontent.com/vitrivr/vitrivr-engine/main/vitrivr-engine-server/doc/oas.json"

openApiGenerate {
    generateApiTests = false
    generateModelTests = false
    validateSpec = false
    skipValidateSpec = true

    generatorName = 'kotlin'
    remoteInputSpec = oasFile
    outputDir = engineBuild
    configOptions = [
            npmName: '@vitrivr-engine/api',
            enumPropertyNaming: 'original'
    ]
}



tasks.test {
    useJUnitPlatform()
}

group = "org.archipanion"
version = "1.0-SNAPSHOT"